/**
    The game logic for Pong
    Pong is played with two paddles on opposite sides of the screen. Players rally the ball until it passes a paddle,
    Where the opposing player scores a point
    play continues until 21 points, and the winner is announced
**/

class PongGame {
    field Ball ball; //the ball between players
    field int leftScore; //the current score of the left player
    field int rightScore; //the current score of the right player
    field Paddle leftPaddle; //the left player/paddle. Uses up/down keys
    field Paddle rightPaddle; // the right player/paddle. uses w/s keys

    constructor PongGame new() {
        //initialize the parts of the game
        let leftScore = 0;
        let rightScore = 0;
        let ball = Ball.new(1, 2);
        let leftPaddle = Paddle.new(2);
        let rightPaddle = Paddle.new(507);
        return this;
    }

    method void dispose() {
        do ball.dispose();
        do rightPaddle.dispose();
        do leftPaddle.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        while ((~(leftScore = 21)) & (~(rightScore = 21))) {
            do run_round();
            do Sys.wait(500); // give the players a chance to see the end
        }
        return;
    }

    method void run_round() {
        var boolean oob;
        var char key;
        var int spin;
        var int ballX;
        var int ballY;
        let oob = false;
        do showScore();
        do ball.reset();
        do leftPaddle.reset();
        do rightPaddle.reset();
        while (~oob) {
            let key = Keyboard.keyPressed();
            let oob = ball.update();
            do Screen.drawLine(255, 0, 255, 255);
            if (key = 131) {
                //up arrow
                do rightPaddle.moveUp();
            }
            if (key = 133) {
                //down arrow
                do rightPaddle.moveDown();
            }
            if (key = 87) {
                //w key
                do leftPaddle.moveUp();
            }
            if (key = 83) {
                //s key
                do leftPaddle.moveDown();
            }
            let ballX = ball.x();
            let ballY = ball.y();
            if ((ballX < 8) & (leftPaddle.connects(ballY))) {
                let spin = leftPaddle.spin(ballY);
                do ball.bounce(spin);
            }
            if ((ball.x() > 502) & (rightPaddle.connects(ballY))) {
                let spin  = rightPaddle.spin(ballY);
                do ball.bounce(spin);
            }
            do Sys.wait(15);
        }
        if (ball.x() > 256) {
            let leftScore = leftScore + 1;
        } else {
            let rightScore = rightScore + 1;
        }

        do showScore();
        return;
    }

    method void showScore() {
        do Screen.drawLine(255, 0, 255, 255);
        do Output.moveCursor(0, 31);
        do Output.printInt(leftScore);
        do Output.printString("-");
        do Output.printInt(rightScore);
        return;
    }
}
